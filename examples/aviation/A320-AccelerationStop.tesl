// Airplane acceleration-stop sequence
// Data extracted from Airbus Flight Crew Performance Course
// A318/A319/A320/A321 Performance Training Manual

// Environment assumptions:
//   Paris Orly (ORY/LFPO)
//   T/O RWY 08
//   DRY conditions
//   TAILWIND -10 kt
//   WEIGHT 55.8 tons
//   Flaps 1 (CONF 1+F)
//   Maximum takeoff thrust
//   V1 = 118 kt
//   ASDA = 1500 m

rational-clock time-S              // in [s]
rational-clock speed-MPS           // in [m.s⁻¹]
rational-clock speed-KT            // in [kt]
rational-quantity distance-M       // in [m]
rational-quantity speed-MPS-DECEL
rational-quantity speed-KT-DECEL
rational-quantity distance-M-DECEL

// Unit conversion between [kt] and [m.s⁻¹]
tag relation speed-KT = <3600/1852> * speed-MPS
tag relation speed-KT-DECEL = <3600/1852> * speed-MPS-DECEL

// V1 threshold
V1-reach sporadic 118.0 on speed-KT

// 1. Uniform acceleration of 4.5 kt/s
tag relation speed-KT = 4.5 * time-S

// Compute run distance with time-variant integration: dx = v.dt
tag relation zero = 0.0
tag relation ddistance-M = der distance-M
tag relation dtime-S = der time-S
tag relation speed-MPS_dtime-S = speed-MPS * dtime-S + zero
tag relation ddistance-M = speed-MPS_dtime-S

// 2. Uniform deceleration of -3 kt/s with ODE dv = ad.dt
tag relation ad = -3.0
tag relation dspeed-KT-DECEL = der speed-KT-DECEL
tag relation ad-dtime-S = ad * dtime-S + zero
tag relation dspeed-KT-DECEL = ad-dtime-S

// Again, compute run distance when decelerating: dx = v.dt
tag relation ddistance-M-DECEL = der distance-M-DECEL
tag relation speed-MPS_dtime-S-DECEL = speed-MPS-DECEL * dtime-S + zero
tag relation ddistance-M-DECEL = speed-MPS_dtime-S-DECEL

RTO sporadic 20.0 on time-S                      // Reject takeoff at 20 s
time-S periodic 2.5                              // Simulation step
						        
@policy asap                                     // Execute ASAP
@scenario 1 (distance-M -> 0.0)                  // Initial condition of ODE
@run until RTO                                   // Acceleration until RTO
@scenario now (distance-M-DECEL -> distance-M)   // Sync distances
@scenario now (speed-KT-DECEL -> speed-KT)       // Sync speeds
@maxstep 23					        
@run                                             // Deceleration
@print
//@output pdf select time-S speed-MPS speed-KT distance-M speed-MPS-DECEL speed-KT-DECEL distance-M-DECEL V1-reach RTO

// At the 23rd simulation step, speed has completely decelerated.
// Approximated acceleration-stop distance is 1159 meters.
