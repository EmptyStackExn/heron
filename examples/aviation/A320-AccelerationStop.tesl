// Airplane acceleration-stop sequence
// Data extracted from Airbus Flight Crew Performance Course
// A318/A319/A320/A321 Performance Training Manual

// Environment assumptions:
//   Paris Orly (ORY/LFPO)
//   T/O RWY 08
//   DRY conditions
//   TAILWIND -10 kt
//   WEIGHT 55.8 tons
//   Flaps 1 (CONF 1+F)
//   Maximum takeoff thrust
//   ASDA = 1500 m

// Performance speeds (IAS):
//   V1 = 118 kt

rational-clock time-S     // in [s]
rational-clock speed-MPS  // in [m.s⁻¹]
rational-clock speed-KT   // in [kt]
rational-quantity speed-KT-DECEL
rational-quantity speed-MPS-DECEL
rational-quantity distance-M // in [m]
rational-quantity distance-M-DECEL // in [m]

// dx = v.dt
tag relation zero = 0.0
tag relation ddistance-M = der distance-M
tag relation dtime-S = der time-S
tag relation speed-MPS_dtime-S = speed-MPS * dtime-S + zero
tag relation ddistance-M = speed-MPS_dtime-S

time-S periodic 2.5

// Unit conversion between [kt] and [m.s⁻¹]
tag relation speed-KT = <3600/1852> * speed-MPS
tag relation speed-KT-DECEL = <3600/1852> * speed-MPS-DECEL

// Uniform acceleration of 4.5 kt/s
tag relation speed-KT = 4.5 * time-S

// V1 threshold
V1-reach sporadic 118.0 on speed-KT

// Deceleration of -3 kt/s
// dv = ad.dt
RTO sporadic 20.0 on time-S
tag relation ad = -3.0
tag relation dspeed-KT-DECEL = der speed-KT-DECEL
tag relation ad-dtime-S = ad * dtime-S + zero
tag relation dspeed-KT-DECEL = ad-dtime-S

// Again, dx = v.dt
tag relation ddistance-M-DECEL = der distance-M-DECEL
tag relation speed-MPS_dtime-S-DECEL = speed-MPS-DECEL * dtime-S + zero
tag relation ddistance-M-DECEL = speed-MPS_dtime-S-DECEL


@policy asap
@scenario 1 (distance-M -> 0.0) // Initial condition
@run until RTO
@scenario now (distance-M-DECEL -> distance-M)
@scenario now (speed-KT-DECEL -> speed-KT)
@maxstep 23
@run
@output pdf select time-S speed-MPS speed-KT distance-M speed-MPS-DECEL speed-KT-DECEL distance-M-DECEL V1-reach RTO

// At the 23rd simulation step, speed has completely decelerated.
// Approximated acceleration-stop distance is 1159 meters.
