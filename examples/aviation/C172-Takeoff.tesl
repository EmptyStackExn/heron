// Airplane takeoff of a Cessna 172

// The following specification illustrates the TESL language with the
// simple takeoff scenario of a light aircraft. It depicts the
// computation of quantities and units with respect to time, speed and
// altitude.

// Performance speeds:
//   VR = 55 kt
//   Vertical speed in climb: 1200 ft/min

rational-clock time-S       // in [s]
rational-clock time-MIN     // in [min]
rational-clock speed-MPS    // in [m.s^-1]
rational-clock speed-KT     // in [kt]
rational-clock altitude-FT  // in [ft]

// Unit conversion between [s] and [min]
tag relation time-S = 60. * time-MIN
// ... and between [kt] and [m.s^-1]
tag relation speed-KT = <3600/1852> * speed-MPS

// Uniform acceleration of 4.5 kt/s
tag relation speed-KT = 4.5 * time-S

// Liftoff occurs instantaneously at rotation speed VR = 55 kt
VR-reach sporadic 55.0 on speed-KT
VR-reach implies liftoff

// Climbing with vertical speed of 1200 ft/min
tag relation altitude-FT = 1200.0 * time-MIN + -244.4

// Flaps retracting occurs at altitude 400 ft
liftoff time delayed by 400. on altitude-FT implies flaps-retract

@scenario 1 (time-S -> 0.0)
@policy asap
@run
@print
@output tex
