// Airplane takeoff

// Physical assumptions:
//   Boeing 737-800
//   Wet runway
//   Maximum takeoff thrust
//   Average weight (75 tons = 150e3 lbs)
//   Flaps 1

// Estimated takeoff speeds:
//   V1 = 110 kt
//   VR = 135 kt

// Assuming uniform acceleration:
//   a = (270 [km.h^-1]) / (40 [s])
//     = 1.875 [m.s^-2]

rational-clock time-SI   // in [s]
rational-clock speed-SI  // in [m.s^-1]
rational-clock speed-KT  // in [kt]

// Uniform acceleration
tag relation speed-SI = 1.875 * time-SI

// Unit conversion between [m.s^-1] and [kt]
tag relation speed-SI = 1.944 * speed-KT

// Speed thresholds
V1-reach strictly precedes VR-reach
V1-reach sporadic 110. on speed-KT
VR-reach sporadic 135. on speed-KT

// Takeoff abortion is forbidden after reaching V1
V1-reach kills stop

// Aborting takeoff prevents from reaching takeoff speeds
stop kills V1-reach

// Liftoff occurs 3s after reaching VR
VR-reach time delayed by 3. on time-SI implies liftoff

// Human excitement measured in some unit
int-clock human-excitement

// Before reaching V1 (time â‰¤ 114.04 sec), engage stop procedure
// stop sporadic 90. on time-SI
// @maxstep 8

@scenario 1 (time-SI -> 0.0)
@policy asap
@run
@print