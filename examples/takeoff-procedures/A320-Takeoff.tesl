// Airplane takeoff of an Airbus A320

// Physical assumptions:
//   Wet runway
//   Maximum takeoff thrust
//   Average weight (75 tons = 150e3 lbs)
//   Flaps 1

// Takeoff speeds
// Extracted from the A320 Flight Crew Operating Manual:
//   V1 = 110 kt
//   VR = 135 kt

rational-clock time-S     // in [s]
rational-clock speed-MPS  // in [m.s^-1]
rational-clock speed-KT   // in [kt]

// Unit conversion between [m.s^-1] and [kt]
tag relation speed-KT = <3600/1852> * speed-MPS

// Assuming uniform acceleration:
//   a = (270 [km.h^-1]) / (40 [s])
//     = 1.875 [m.s^-2]
tag relation speed-MPS = 1.875 * time-S

// Speed thresholds
V1-reach strictly precedes VR-reach
V1-reach sporadic 110. on speed-KT
VR-reach sporadic 135. on speed-KT

// Liftoff occurs 3s after reaching VR
VR-reach time delayed by 3. on time-S implies liftoff

ABRK-active sporadic 72.0 on speed-KT
ABRK-on  strictly precedes ABRK-active
SPLR-arm strictly precedes ABRK-active

RTO sustained from ABRK-active to ABRK-off implies brakes-apply
ABRK-active strictly precedes brakes-apply

// Takeoff rejection (RTO) is forbidden after reaching V1
// Rejecting takeoff prevents from reaching takeoff speeds
V1-reach kills RTO
brakes-apply kills V1-reach

// --- SCENARIO OPTIONS ---
// (a) Explicit events when 0.0s
@scenario 1 (time-S -> 0.0)

// (b) Before reaching V1, engage No Go procedure
// RTO sporadic 25. on time-S

// (c) Input observation, brakes not applied at instant 3
// @scenario strict 3 RTO

// (d) Execute a chonometer in parallel
// chronometer periodic 1.0
// tag relation chronometer = time-S

@maxstep 5
@policy asap
@run
@print
@output tex select RTO liftoff
